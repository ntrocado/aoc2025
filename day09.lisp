(defun area (x1 y1 x2 y2)
  (* (1+ (abs (- x1 x2))) (1+ (abs (- y1 y2)))))

(defun parse-input (&optional (file "day09-input.txt"))
  (iter (for l in-file file using #'read-line)
    (collect (mapcar #'parse-integer (uiop:split-string l :separator '(#\,))))))

(defun point-in-line-p (x y x1 y1 x2 y2)
  (or (and (or (<= x1 x x2) (>= x1 x x2))
	   (= y y1 y2))
      (and (or (<= y1 y y2) (>= y1 y y2))
	   (= x x1 x2))))

(defun point-inside-p (x y tiles)
  (oddp
   (iter (for ((x1 y1) (x2 y2)) on tiles)
     (while x2)
     (when (point-in-line-p x y x1 y1 x2 y2)
       (return-from point-inside-p t))
     (when (and (/= y1 y2)
                (or (and (< y1 y) (<= y y2))
                    (and (< y2 y) (<= y y1))))
       (when (<= (+ x1 (/ (* (- x2 x1) (- y y1)) (- y2 y1))) x)
         (counting t))))))

(defun segments-intersect-p (x1 y1 x2 y2 x3 y3 x4 y4)
  (let ((denom (- (* (- x1 x2) (- y3 y4))
                  (* (- y1 y2) (- x3 x4)))))
    (when (not (zerop denom))
      (let ((t-val (/ (- (* (- x1 x3) (- y3 y4))
                         (* (- y1 y3) (- x3 x4)))
                      denom))
            (u-val (/ (- (* (- x1 x3) (- y1 y2))
                         (* (- y1 y3) (- x1 x2)))
                      denom)))
        (and (< 0 t-val 1) (< 0 u-val 1))))))

(defun rectangle-inside-p (x1 y1 x2 y2 tiles)
  (let ((min-x (min x1 x2)) (max-x (max x1 x2))
        (min-y (min y1 y2)) (max-y (max y1 y2)))
    ;; Check all corners are inside
    (unless (and (point-inside-p min-x min-y tiles)
                 (point-inside-p max-x min-y tiles)
                 (point-inside-p min-x max-y tiles)
                 (point-inside-p max-x max-y tiles))
      (return-from rectangle-inside-p nil))
    ;; Check no rectangle edge crosses polygon edges
    (iter (for ((px1 py1) (px2 py2)) on tiles)
      (while px2)
      (never (or (segments-intersect-p min-x min-y max-x min-y px1 py1 px2 py2)
                 (segments-intersect-p max-x min-y max-x max-y px1 py1 px2 py2)
                 (segments-intersect-p max-x max-y min-x max-y px1 py1 px2 py2)
                 (segments-intersect-p min-x max-y min-x min-y px1 py1 px2 py2))))))

(defun largest-inside-area (input)
  (let ((tiles (append input (list (first input)))))
    (iter outer
      (for coords on input)
      (iter (for (x1 y1) = (first coords))
        (for (x2 y2) in (rest coords))
        (for a = (area x1 y1 x2 y2))
        (in outer (maximizing a into part1))
        (when (rectangle-inside-p x1 y1 x2 y2 tiles)
          (in outer (maximizing a into part2))))
      (finally (return-from outer (values part1 part2))))))
